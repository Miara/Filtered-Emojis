buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()

    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath(libs.plugin.android.cache.fix)
    classpath(libs.plugin.androidgradleplugin)
    classpath(libs.plugin.dokka)
    classpath(libs.plugin.kotlin)
    classpath(libs.plugin.licensee)
    classpath(libs.plugin.metalava)
    classpath(libs.plugin.publish)
  }
}

plugins {
  alias(libs.plugins.codequalitytools)
  alias(libs.plugins.dependencygraphgenerator)
}

codeQualityTools {
  lint {
    lintConfig = rootProject.file("lint.xml")
    checkAllWarnings = true
    textReport = true
  }
  checkstyle {
    enabled = false // Kotlin only.
  }
  pmd {
   enabled = false // Kotlin only.
  }
  ktlint {
    toolVersion = libs.versions.ktlint.get()
    experimental = true
  }
  detekt {
    enabled = false
  }
  cpd {
    enabled = false
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'https://jitpack.io'}
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    gradlePluginPortal()
  }
}

subprojects {
  plugins.withType(com.android.build.gradle.api.AndroidBasePlugin) {
    project.apply plugin: "org.gradle.android.cache-fix"
  }

  project.tasks.withType(Test) {
    testLogging {
      testLogging.exceptionFormat = 'full'
    }
  }


}


configure(subprojects) { subproject ->

  plugins.withType(MavenPublishPlugin) {
    afterEvaluate {
      publishing {
        publications {
          maven(MavenPublication) {
            if (plugins.hasPlugin(JavaLibraryPlugin)) {
              from components.java
            } else {
              from components.release // should be defined in android lib gradle
            }


            groupId = 'com.vanniktech.miara'
            artifactId = subproject.name
            version = "0.18.0"
          }
        }
      }
    }
  }
}